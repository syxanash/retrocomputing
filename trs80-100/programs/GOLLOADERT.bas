0 CLS
10 INPUT "ENTER COLUMN SIZE"; COLS
20 INPUT "ENTER ROWS SIZE"; ROWS
30 GRID = COLS * ROWS
40 DIM CGEN(GRID)
50 DIM NGEN(GRID)
60 INPUT "ENTER RANDOM SEED (1 TO 5 DIGITS)"; SEED%
70 LF = INT(RND(SEED%*-2))
80 PRINT "INITIALISING GRID..."
90 FOR I = 0 TO GRID
100 LF = INT(2*RND(1))
110 CGEN(I) = LF
120 NEXT I
130 CLS
140 FOR I = 0 TO GRID-1
150 IF CGEN(I) = 0 THEN PRINT " "; ELSE PRINT "@";
160 IF ((I+1) MOD COLS) = 0 THEN PRINT ""
170 NEXT I
180 GOSUB 570
190 RC = 0
200 BOXCO# = 0
210 MABOX = COLS - 1
220 PASSO# = MABOX / GRID
230 FOR I = 0 TO GRID-1
240 GOSUB 510
250 NGB = 0
260 TR = (((RC - 1) + ROWS) MOD ROWS)
270 BR = ((RC + 1) MOD ROWS)
280 A = ((((I - 1) MOD COLS) + COLS) MOD COLS) + (COLS*RC)
290 B = ((I + 1) MOD COLS) + (COLS*RC)
300 C = ((((I - COLS - 1) MOD COLS) + COLS) MOD COLS) + (COLS*TR)
310 D = ((((I - COLS + 1) MOD COLS) + COLS) MOD COLS) + (COLS*TR)
320 E = ((((I - COLS) MOD COLS) + COLS) MOD COLS) + (COLS*TR)
330 F = ((I + COLS) MOD COLS) + (COLS*BR)
340 G = ((I + COLS + 1) MOD COLS) + (COLS*BR)
350 H = ((I + COLS - 1) MOD COLS) + (COLS*BR)
360 NGB = CGEN(A) + CGEN(B) + CGEN(C) + CGEN(D) + CGEN(E) + CGEN(F) + CGEN(G) + CGEN(H)
370 IF CGEN(I) = 1 AND NGB < 2 THEN NGEN(I) = 0
380 IF CGEN(I) = 1 AND (NGB = 2 OR NGB = 3) THEN NGEN(I) = 1
390 IF CGEN(I) = 1 AND NGB > 3 THEN NGEN(I) = 0
400 IF CGEN(I) = 0 AND NGB = 3 THEN NGEN(I) = 1
410 IF ((I + 1) MOD COLS) = 0 THEN RC = RC + 1
420 NEXT I
430 CLS
440 FOR I = 0 TO GRID-1
450 IF NGEN(I) = 0 THEN PRINT " "; ELSE PRINT "@";
460 IF ((I+1) MOD COLS) = 0 THEN PRINT ""
470 CGEN(I) = NGEN(I)
480 NEXT I
490 GOSUB 570
500 GOTO 190
510 REM FUNCTION INC_LOADER
520 BOXCO# = (BOXCO# + PASSO#)
530 FOR J = 0 TO INT(BOXCO#)
540 PRINT @280 + J, CHR$(239);
550 NEXT J
560 RETURN
570 REM FUNCTION PRINT_BAR
580 FOR J = 0 TO COLS-1
590 PRINT @280 + J, CHR$(255);
600 NEXT J
610 RETURN