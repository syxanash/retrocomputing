0 CLS
10 CUCO = 0
20 LC$(0) = "/"
30 LC$(1) = "-"
40 LC$(2) = "\"
50 LC$(3) = CHR$(245)
60 INPUT "ENTER COLUMN SIZE"; COLS
70 INPUT "ENTER ROWS SIZE"; ROWS
80 GRID = COLS * ROWS
90 DIM CGEN(GRID)
100 DIM NGEN(GRID)
110 INPUT "ENTER RANDOM SEED (1 TO 5 DIGITS)"; SEED%
120 LF = INT(RND(SEED%*-2))
130 PRINT "INITIALISING GRID..."
140 FOR I = 0 TO GRID
150 LF = INT(2*RND(1))
160 CGEN(I) = LF
170 NGEN(I) = CGEN(I)
180 NEXT I
190 CLS
200 BEEP
210 FOR I = 0 TO GRID-1
220 IF NGEN(I) = 0 THEN PRINT " "; ELSE PRINT "@";
230 IF ((I+1) MOD COLS) = 0 THEN PRINT ""
240 CGEN(I) = NGEN(I)
250 NEXT I
260 RC = 0
270 FOR I = 0 TO GRID-1
280 GOSUB 480
290 NGB = 0
300 TR = (((RC - 1) + ROWS) MOD ROWS)
310 BR = ((RC + 1) MOD ROWS)
320 CC = I + COLS
330 A = ((CC - 1) MOD COLS) + (COLS*RC)
340 B = ((CC + 1) MOD COLS) + (COLS*RC)
350 C = ((CC - 1) MOD COLS) + (COLS*TR)
360 D = ((CC + 1) MOD COLS) + (COLS*TR)
370 E = (CC MOD COLS) + (COLS*TR)
380 F = (CC MOD COLS) + (COLS*BR)
390 G = ((CC + 1) MOD COLS) + (COLS*BR)
400 H = ((CC - 1) MOD COLS) + (COLS*BR)
410 NGB = CGEN(A) + CGEN(B) + CGEN(C) + CGEN(D) + CGEN(E) + CGEN(F) + CGEN(G) + CGEN(H)
420 IF NGB < 2 THEN NGEN(I) = 0
430 IF NGB > 3 THEN NGEN(I) = 0
440 IF NGEN(I) = 0 AND NGB = 3 THEN NGEN(I) = 1
450 IF ((I + 1) MOD COLS) = 0 THEN RC = RC + 1
460 NEXT I
470 GOTO 190
480 REM FUNCTION PRINT_LOADER
490 PRINT @318, LC$(CUCO);
500 CUCO = (CUCO + 1) MOD 4
510 RETURN