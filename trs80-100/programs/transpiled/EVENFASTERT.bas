0 CLS
10 CUCO = 0
20 LC$(0) = "/"
30 LC$(1) = "-"
40 LC$(2) = "\"
50 LC$(3) = CHR$(245)
60 INPUT "ENTER COLUMN SIZE"; COLS
70 INPUT "ENTER ROWS SIZE"; ROWS
80 GRID = COLS * ROWS
90 DIM CGEN(GRID)
100 DIM NGEN(GRID)
110 INPUT "ENTER RANDOM SEED (1 TO 5 DIGITS)"; SEED%
120 LF = INT(RND(SEED%*-2))
130 PRINT "INITIALISING GRID..."
140 FOR I = 0 TO GRID
150 LF = INT(2*RND(1))
160 CGEN(I) = LF
170 NGEN(I) = CGEN(I)
180 NEXT I
190 CLS
200 BEEP
210 CC = 0
220 RC = 0
230 FOR I = 0 TO GRID-1
240 IF NGEN(I) = 0 THEN PRINT " "; ELSE PRINT "@";
250 IF ((I+1) MOD COLS) = 0 THEN PRINT ""
260 CGEN(I) = NGEN(I)
270 NEXT I
280 FOR I = 0 TO GRID-1
290 GOSUB 670
300 NGB = 0
310 IND = 0
320 IND = I - COLS
330 IF (RC = 0) THEN IND = IND + GRID
340 NGB = NGB + CGEN(IND)
350 IND = (I - COLS) + 1
360 IF (RC = 0) THEN IND = IND + GRID
370 IF (CC = COLS-1) THEN  IND = IND - COLS
380 NGB = NGB + CGEN(IND)
390 IND = (I - COLS) - 1
400 IF (RC = 0) THEN IND = IND + GRID
410 IF (CC = 0) THEN IND = IND + COLS
420 NGB = NGB + CGEN(IND)
430 IND = I + COLS
440 IF (RC = (ROWS - 1)) THEN	IND = IND - GRID
450 NGB = NGB + CGEN(IND)
460 IND = (I + COLS) + 1
470 IF (RC = (ROWS - 1)) THEN	IND = IND - GRID
480 IF (CC = (COLS - 1)) THEN	IND = IND - COLS
490 NGB = NGB + CGEN(IND)
500 IND = (I + COLS) - 1
510 IF (RC = (ROWS - 1)) THEN	IND = IND - GRID
520 IF (CC = 0) THEN IND = IND + COLS
530 NGB = NGB + CGEN(IND)
540 IND = I + 1
550 IF (CC = (COLS - 1)) THEN IND = IND - COLS
560 NGB = NGB + CGEN(IND)
570 IND = I - 1
580 IF (CC = 0) THEN IND = IND + COLS
590 NGB = NGB + CGEN(IND)
600 IF NGB < 2 THEN NGEN(I) = 0
610 IF NGB > 3 THEN NGEN(I) = 0
620 IF NGEN(I) = 0 AND NGB = 3 THEN NGEN(I) = 1
630 CC = (CC + 1) MOD COLS
640 IF ((I + 1) MOD COLS) = 0 THEN RC = RC + 1
650 NEXT I
660 GOTO 190
670 REM FUNCTION PRINT_LOADER
680 PRINT @318, LC$(CUCO);
690 CUCO = (CUCO + 1) MOD 4
700 RETURN