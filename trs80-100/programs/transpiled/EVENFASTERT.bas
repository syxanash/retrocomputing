0 CLS
10 INPUT "ENTER COLUMN SIZE"; COLS
20 INPUT "ENTER ROWS SIZE"; ROWS
30 GRID = COLS * ROWS
40 DIM CGEN(GRID)
50 DIM NGEN(GRID)
60 INPUT "ENTER RANDOM SEED (1 TO 5 DIGITS)"; SEED%
70 LF = INT(RND(SEED%*-2))
80 PRINT "INITIALISING GRID..."
90 FOR I = 0 TO GRID
100 LF = INT(2*RND(1))
110 CGEN(I) = LF
120 NGEN(I) = CGEN(I)
130 NEXT I
140 CLS
150 BEEP
160 CC = 0
170 RC = 0
180 FOR I = 0 TO GRID-1
190 IF NGEN(I) = 0 THEN PRINT " "; ELSE PRINT "@";
200 IF ((I+1) MOD COLS) = 0 THEN PRINT ""
210 CGEN(I) = NGEN(I)
220 NEXT I
230 FOR I = 0 TO GRID-1
240 PRINT @280, "...";
250 NGB = 0
260 IND = 0
270 IND = I - COLS
280 IF (RC = 0) THEN IND = IND + GRID
290 NGB = NGB + CGEN(IND)
300 IND = (I - COLS) + 1
310 IF (RC = 0) THEN IND = IND + GRID
320 IF (CC = COLS-1) THEN  IND = IND - COLS
330 NGB = NGB + CGEN(IND)
340 IND = (I - COLS) - 1
350 IF (RC = 0) THEN IND = IND + GRID
360 IF (CC = 0) THEN IND = IND + COLS
370 NGB = NGB + CGEN(IND)
380 IND = I + COLS
390 IF (RC = (ROWS - 1)) THEN	IND = IND - GRID
400 NGB = NGB + CGEN(IND)
410 IND = (I + COLS) + 1
420 IF (RC = (ROWS - 1)) THEN	IND = IND - GRID
430 IF (CC = (COLS - 1)) THEN	IND = IND - COLS
440 NGB = NGB + CGEN(IND)
450 IND = (I + COLS) - 1
460 IF (RC = (ROWS - 1)) THEN	IND = IND - GRID
470 IF (CC = 0) THEN IND = IND + COLS
480 NGB = NGB + CGEN(IND)
490 IND = I + 1
500 IF (CC = (COLS - 1)) THEN IND = IND - COLS
510 NGB = NGB + CGEN(IND)
520 IND = I - 1
530 IF (CC = 0) THEN IND = IND + COLS
540 NGB = NGB + CGEN(IND)
550 IF NGB < 2 THEN NGEN(I) = 0
560 IF NGB > 3 THEN NGEN(I) = 0
570 IF NGEN(I) = 0 AND NGB = 3 THEN NGEN(I) = 1
580 CC = (CC + 1) MOD COLS
590 IF ((I + 1) MOD COLS) = 0 THEN RC = RC + 1
600 NEXT I
610 GOTO 140