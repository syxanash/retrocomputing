0 CLS
10 CUCO = 0
20 LC$(0) = "/"
30 LC$(1) = "-"
40 LC$(2) = "\"
50 LC$(3) = CHR$(245)
60 INPUT "ENTER COLUMN SIZE"; COLS
70 INPUT "ENTER ROWS SIZE"; ROWS
80 GRID = COLS * ROWS
90 DIM CGEN(GRID)
100 DIM NGEN(GRID)
110 INPUT "ENTER RANDOM SEED (1 TO 5 DIGITS)"; SEED%
120 LF = INT(RND(SEED%*-2))
130 PRINT "INITIALISING GRID..."
140 FOR I = 0 TO GRID
150 LF = INT(2*RND(1))
160 CGEN(I) = LF
170 NGEN(I) = CGEN(I)
180 NEXT I
190 CLS
200 FOR I = 0 TO GRID-1
210 IF NGEN(I) = 0 THEN PRINT " "; ELSE PRINT "@";
220 IF ((I+1) MOD COLS) = 0 THEN PRINT ""
230 CGEN(I) = NGEN(I)
240 NEXT I
250 RC = 0
260 FOR I = 0 TO GRID-1
270 GOSUB 470
280 NGB = 0
290 TR = (((RC - 1) + ROWS) MOD ROWS)
300 BR = ((RC + 1) MOD ROWS)
310 A = (((((I - 1) MOD COLS)) + COLS) MOD COLS) + (COLS*RC)
320 B = ((I + 1) MOD COLS) + (COLS*RC)
330 C = (((((I - COLS - 1) MOD COLS)) + COLS) MOD COLS) + (COLS*TR)
340 D = (((((I - COLS + 1) MOD COLS)) + COLS) MOD COLS) + (COLS*TR)
350 E = (((((I - COLS) MOD COLS)) + COLS) MOD COLS) + (COLS*TR)
360 F = ((I + COLS) MOD COLS) + (COLS*BR)
370 G = ((I + COLS + 1) MOD COLS) + (COLS*BR)
380 H = ((I + COLS - 1) MOD COLS) + (COLS*BR)
390 NGB = CGEN(A) + CGEN(B) + CGEN(C) + CGEN(D) + CGEN(E) + CGEN(F) + CGEN(G) + CGEN(H)
400 IF CGEN(I) = 1 AND NGB < 2 THEN NGEN(I) = 0
410 IF CGEN(I) = 1 AND (NGB = 2 OR NGB = 3) THEN NGEN(I) = 1
420 IF CGEN(I) = 1 AND NGB > 3 THEN NGEN(I) = 0
430 IF CGEN(I) = 0 AND NGB = 3 THEN NGEN(I) = 1
440 IF ((I + 1) MOD COLS) = 0 THEN RC = RC + 1
450 NEXT I
460 GOTO 190
470 REM FUNCTION PRINT_LOADER
480 PRINT @318, LC$(CUCO);
490 CUCO = (CUCO + 1) MOD 4
500 RETURN