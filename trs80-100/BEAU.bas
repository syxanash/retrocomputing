5 CLS
10 CUCO = 0
20 LC$(0) = "/"
30 LC$(1) = "-"
40 LC$(2) = "\"
50 LC$(3) = "|"
60 INPUT "ENTER COLUMN SIZE"; COLS
70 INPUT "ENTER ROWS SIZE"; ROWS
80 GRID = COLS * ROWS
90 DIM CGEN(GRID)
100 DIM NGEN(GRID)
110 INPUT "ENTER RANDOM SEED"; SEED%
120 LF = INT(RND(SEED%*-2))
130 PRINT "INITIALISING GRID..."
140 FOR I = 0 TO GRID
150 LF = INT(2*RND(1))
160 CGEN(I) = LF
170 NEXT I
180 CLS
190 FOR I = 0 TO GRID-1
200 IF CGEN(I) = 0 THEN PRINT " "; ELSE PRINT "@";
210 IF ((I+1) MOD COLS) = 0 THEN PRINT ""
220 NEXT I
230 CC = 0
240 RC = 0
250 FOR I = 0 TO GRID-1
260 GOSUB 530
270 NGB = 0
280 TR = (((RC - 1) + ROWS) MOD ROWS)
290 BR = ((RC + 1) MOD ROWS)
300 A = (((((I - 1) MOD COLS)) + COLS) MOD COLS) + (COLS*RC)
310 B = ((I + 1) MOD COLS) + (COLS*RC)
320 C = (((((I - COLS - 1) MOD COLS)) + COLS) MOD COLS) + (COLS*TR)
330 D = (((((I - COLS + 1) MOD COLS)) + COLS) MOD COLS) + (COLS*TR)
340 E = (((((I - COLS) MOD COLS)) + COLS) MOD COLS) + (COLS*TR)
350 F = ((I + COLS) MOD COLS) + (COLS*BR)
360 G = ((I + COLS + 1) MOD COLS) + (COLS*BR)
370 H = ((I + COLS - 1) MOD COLS) + (COLS*BR)
380 NGB = CGEN(A) + CGEN(B) + CGEN(C) + CGEN(D) + CGEN(E) + CGEN(F) + CGEN(G) + CGEN(H)
390 IF CGEN(I) = 1 AND NGB < 2 THEN NGEN(I) = 0
400 IF CGEN(I) = 1 AND (NGB = 2 OR NGB = 3) THEN NGEN(I) = 1
410 IF CGEN(I) = 1 AND NGB > 3 THEN NGEN(I) = 0
420 IF CGEN(I) = 0 AND NGB = 3 THEN NGEN(I) = 1
430 CC = (CC + 1) MOD COLS
440 IF ((I + 1) MOD COLS) = 0 THEN RC = RC + 1
450 NEXT I
460 CLS
470 FOR I = 0 TO GRID-1
480 IF NGEN(I) = 0 THEN PRINT " "; ELSE PRINT "@";
490 IF ((I+1) MOD COLS) = 0 THEN PRINT ""
500 CGEN(I) = NGEN(I)
510 NEXT I
520 GOTO 230
530 REM FUNCTION PRINT_LOADER
540 PRINT @318, LC$(CUCO);
550 CUCO = (CUCO + 1) MOD 4
560 RETURN